\ Y: Instruction Pointer
\ X: temporary W register
\ U: Param Stack
\ S: Return Stack

code execute
  pulU x       ; arg -> W
  ldd 0,x      ; goto W+[W]
  jmp D,X
  ;
 
code dup
  ldd 0,u
  std ,--u
  ;

code +
  ldd ,u++
  addd 0,u
  std 0,u
  ;

code .
  ldb #32
  jsr putchar,pcr
  ldd ,u++
  jsr PrintD,pcr
  ldb #13
  jsr putchar,pcr
  ;

code lit
  ldd ,Y++  ; get next cell from IP
  std ,--u  ; and stack it.
  ;

code exit
  pulS y       ; pop previous IP.
  jmp Next,pcr ; and keep going.
  ;
  

code bye
OsExit
  ldb #13  ; CR
  jsr putchar,pcr
  ldb #35  ; #
  jsr putchar,pcr
  ldb #13  ; CR
  jsr putchar,pcr
  clrb
  ldb #8
  os9 F$Exit
  ;

: double
    dup + ;
: run
    $2 dup . double dup . double dup . double . bye ;

